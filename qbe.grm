structure T = QbeTypes

%%
%name Qbe
%term
    EOF
  | TYPE | ALIGN | EXPORT | SECTION | DATA | FUNCTION
  | ADD | SUB | DIV | MUL | NEG | UDIV | REM | UREM | OR | XOR | AND | SAR | SHR
  | SHL
  | STORED | STORES | STOREL | STOREW | STOREH | STOREB
  | LOADD | LOADS | LOADL | LOADW | LOADSW | LOADUW | LOADSH | LOADUH | LOADSB
  | LOADUB
  | ALLOC4 | ALLOC8 | ALLOC16
  | CEQD | CEQL | CEQS | CEQW | CGED | CGES | CGTD | CGTS | CLED | CLES | CLTD 
  | CLTS | CNED | CNEL | CNES | CNEW | COD  | COS  | CSGEL | CSGEW | CSGTL
  | CSGTW | CSLEL | CSLEW | CSLTL | CSLTW | CUGEL | CUGEW | CUGTL | CUGTW
  | CULEL | CULEW | CULTL | CULTW | CUOD | CUOS 
  | DTOSI | DTOUI | EXTS  | EXTSB | EXTSH | EXTSW | EXTUB | EXTUH | EXTUW
  | SLTOF | ULTOF | STOSI | STOUI | SWTOF | UWTOF | TRUNCD
  | CAST | COPY
  | CALL
  | VASTART | VAARG
  | PHI
  | JMP | JNZ | RET | RETW
  | NOP
  | W | L | S | D | B | H | Z
  | PLUS | EQ | COMMA | LPAREN | RPAREN | LBRACE | RBRACE | DOTS
  | TYP of Atom.atom
  | GLO of Atom.atom
  | TMP of Atom.atom
  | LBL of Atom.atom
  | INT of int
  | FLTS of real
  | FLTD of real
  | STR of string

%pos int
%pure
%eop EOF
%noshift EOF

%value TYP (Atom.atom ":bogus")
%value GLO (Atom.atom "$bogus")
%value TMP (Atom.atom "%bogus")
%value LBL (Atom.atom "@bogus")
%value INT (1)
%value FLTS (1.0)
%value FLTD (1.0)
%value STR ("")

%keyword TYPE ALIGN EXPORT SECTION DATA FUNCTION
%prefer LBL

%nonterm
    module of T.def list
  | defs of T.def list
  | def of  T.def
  | tylist of (T.ty * int) list
  | ty_num of T.ty * int
  | typ of T.ty
  | linkage of T.linkage
  | section of T.section
  | align_opt of int option
  | data_fields of T.datafield list
  | data_field of T.datafield
  | data_items of T.dataitem list
  | data_item of T.dataitem
  | const of T.const
  | typ_opt of T.ty option
  | params of {params: (T.ty * Atom.atom) list, variadic: bool}
  | param of T.ty * Atom.atom
  | blocks of T.block list
  | block of T.block
  | stmts of T.stmt list
  | stmt of T.stmt
  | jump of T.instr
  | value of T.value
  | ret of T.value option -> T.instr
  | instr1v of T.instr
  | instr2v of T.instr
  | call of T.instr
  | args of (T.ty * T.value) list
  | arg of T.ty * T.value
  | phiargs of (Atom.atom * T.value) list
  | phiarg of Atom.atom * T.value
  | instr1 of T.value -> T.instr
  | instr2 of T.value * T.value -> T.instr

%%

module : defs                           (defs)

defs :                                  ([])
     | def defs                         (def::defs)

def : TYPE TYP EQ LBRACE tylist RBRACE          (T.Type{name=TYP,align=NONE,items=tylist})
    | TYPE TYP EQ ALIGN INT LBRACE tylist RBRACE(T.Type{name=TYP,align=SOME INT,items=tylist})
    | TYPE TYP EQ ALIGN INT LBRACE INT RBRACE   (T.OpaqueType{name=TYP,align=INT1,size=INT2})
    | linkage DATA GLO EQ align_opt LBRACE data_fields RBRACE
        (T.Data {name = GLO,
                 linkage = linkage,
                 align = align_opt,
                 fields = data_fields})
    | linkage FUNCTION typ_opt GLO LPAREN params RPAREN LBRACE blocks RBRACE
        (T.Function {name = GLO,
                     linkage = linkage,
                     params = #params params,
                     variadic = #variadic params,
                     result = typ_opt,
                     blocks = blocks})

tylist :                                  ([])
       | ty_num                           ([ty_num])
       | ty_num COMMA tylist              (ty_num::tylist)

ty_num : typ                              (typ, 1)
       | typ INT                          (typ, INT)

typ : W                                   (T.W)
    | L                                   (T.L)
    | S                                   (T.S)
    | D                                   (T.D)
    | B                                   (T.B)
    | H                                   (T.H)
    | TYP                                 (T.Aggr TYP)

linkage :                                 ({exported=false, section=NONE})
        | EXPORT                          ({exported=true, section=NONE})
        | EXPORT section                  ({exported=true, section=SOME section})
        | section EXPORT                  ({exported=true, section=SOME section})

section : SECTION STR                     ({name=STR, flags=NONE})
        | SECTION STR STR                 ({name=STR1, flags=SOME STR2})

align_opt :                               (NONE)
          | ALIGN INT                     (SOME INT)

data_fields :                             ([])
            | data_field                  ([data_field])
            | data_field COMMA data_fields(data_field::data_fields)

data_field : typ data_items               (T.DataTy(typ, data_items))
           | Z INT                        (T.DataZ INT)

data_items : data_item                    ([data_item])
           | data_item data_items         (data_item::data_items)

data_item : GLO                           (T.DataSym(GLO, 0))
          | GLO PLUS INT                  (T.DataSym(GLO, INT))
          | STR                           (T.DataStr STR)
          | const                         (T.DataCon const)

const : INT                               (T.Int INT)
      | FLTS                              (T.Flts FLTS)
      | FLTD                              (T.Fltd FLTD)

typ_opt :                                 (NONE)
        | typ                             (SOME typ)

params :                                  ({params=[], variadic=false})
       | DOTS                             ({params=[], variadic=true})
       | DOTS COMMA                       ({params=[], variadic=true})
       | param                            ({params=[param], variadic=false})
       | param COMMA params               ({params = param::(#params params),
                                            variadic = #variadic params})

param : typ TMP                           ((typ, TMP))

blocks : block                            ([block])
       | block blocks                     (block::blocks)

block : LBL stmts                         ({label=LBL, stmts=stmts, jump=NONE})
      | LBL stmts jump                    ({label=LBL, stmts=stmts, jump=SOME jump})

stmts :                                   ([])
      | stmt stmts                        (stmt::stmts)

stmt : instr1v                            (T.Volatile instr1v)
     | instr2v                            (T.Volatile instr2v)
     | call                               (T.Volatile call)
     | TMP EQ typ instr1v                 (T.Assign(TMP, typ, instr1v))
     | TMP EQ typ instr2v                 (T.Assign(TMP, typ, instr2v))
     | TMP EQ typ call                    (T.Assign(TMP, typ, call))
     | TMP EQ typ PHI phiargs             (T.Volatile(T.Phi phiargs))
     | NOP                                (T.Volatile T.Nop)

jump : JMP LBL                            (T.Jmp LBL)
     | JNZ value COMMA LBL COMMA LBL      (T.Jnz(value, LBL1, LBL2))
     | ret                                (ret NONE)
     | ret value                          (ret(SOME value))

value : TMP                               (T.Tmp TMP)
      | GLO                               (T.Glo GLO)
      | const                             (T.Con const)

ret : RET                                 (T.Ret)
    | RETW                                (T.Retw)

instr1v : instr1 value                    (instr1 value)

instr2v : instr2 value COMMA value        (instr2(value1, value2))

call : CALL GLO LPAREN args RPAREN        (T.Call(GLO, args))

args :                                    ([])
     | arg                                ([arg])
     | arg COMMA args                     (arg::args)

arg : typ value                           ((typ, value))

phiargs : phiarg                          ([phiarg])
        | phiarg COMMA phiargs            (phiarg::phiargs)

phiarg : LBL value                        ((LBL, value))

instr1 : NEG                              (T.Neg)
       | LOADD                            (T.Loadd)
       | LOADS                            (T.Loads)
       | LOADL                            (T.Loadl)
       | LOADW                            (T.Loadw)
       | LOADSW                           (T.Loadsw)
       | LOADUW                           (T.Loaduw)
       | LOADSH                           (T.Loadsh)
       | LOADUH                           (T.Loaduh)
       | LOADSB                           (T.Loadsb)
       | LOADUB                           (T.Loadub)
       | ALLOC4                           (T.Alloc4)
       | ALLOC8                           (T.Alloc8)
       | ALLOC16                          (T.Alloc16)
       | DTOSI                            (T.Dtosi)
       | DTOUI                            (T.Dtoui)
       | EXTS                             (T.Exts)
       | EXTSB                            (T.Extsb)
       | EXTSH                            (T.Extsh)
       | EXTSW                            (T.Extsw)
       | EXTUB                            (T.Extub)
       | EXTUH                            (T.Extuh)
       | EXTUW                            (T.Extuw)
       | SLTOF                            (T.Sltof)
       | ULTOF                            (T.Ultof)
       | STOSI                            (T.Stosi)
       | STOUI                            (T.Stoui)
       | SWTOF                            (T.Swtof)
       | UWTOF                            (T.Uwtof)
       | TRUNCD                           (T.Truncd)
       | CAST                             (T.Cast)
       | COPY                             (T.Copy)
       | VASTART                          (T.Vastart)
       | VAARG                            (T.Vaarg)

instr2 : ADD                              (T.Add)
       | SUB                              (T.Sub)
       | DIV                              (T.Div)
       | MUL                              (T.Mul)
       | UDIV                             (T.Udiv)
       | REM                              (T.Rem)
       | UREM                             (T.Urem)
       | OR                               (T.Or)
       | XOR                              (T.Xor)
       | AND                              (T.And)
       | SAR                              (T.Sar)
       | SHR                              (T.Shr)
       | SHL                              (T.Shl)
       | STORED                           (T.Stored)
       | STORES                           (T.Stores)
       | STOREL                           (T.Storel)
       | STOREW                           (T.Storew)
       | STOREH                           (T.Storeh)
       | STOREB                           (T.Storeb)
       | CEQD                             (T.Ceqd)
       | CEQL                             (T.Ceql)
       | CEQS                             (T.Ceqs)
       | CEQW                             (T.Ceqw)
       | CGED                             (T.Cged)
       | CGES                             (T.Cges)
       | CGTD                             (T.Cgtd)
       | CGTS                             (T.Cgts)
       | CLED                             (T.Cled)
       | CLES                             (T.Cles)
       | CLTD                             (T.Cltd)
       | CLTS                             (T.Clts)
       | CNED                             (T.Cned)
       | CNEL                             (T.Cnel)
       | CNES                             (T.Cnes)
       | CNEW                             (T.Cnew)
       | COD                              (T.Cod)
       | COS                              (T.Cos)
       | CSGEL                            (T.Csgel)
       | CSGEW                            (T.Csgew)
       | CSGTL                            (T.Csgtl)
       | CSGTW                            (T.Csgtw)
       | CSLEL                            (T.Cslel)
       | CSLEW                            (T.Cslew)
       | CSLTL                            (T.Csltl)
       | CSLTW                            (T.Csltw)
       | CUGEL                            (T.Cugel)
       | CUGEW                            (T.Cugew)
       | CUGTL                            (T.Cugtl)
       | CUGTW                            (T.Cugtw)
       | CULEL                            (T.Culel)
       | CULEW                            (T.Culew)
       | CULTL                            (T.Cultl)
       | CULTW                            (T.Cultw)
       | CUOD                             (T.Cuod)
       | CUOS                             (T.Cuos)
